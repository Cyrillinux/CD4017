* CD4017B R1 ngspice KiCad model
* Cyril BARBATO / cyril.barbato@gmx.com under GPL
* DISLAIMER OF ALL WARRANTIES *
* O6/05/2024 : RO : Not RST circuit
* 10/05/2024 : R1 : RST circuit builded and New diode DDS
* Hybrid circuit MOSFET(CMOS)/BJT(TTL)
* TODO : All MOSFET building
*
* CD4017B *
.subckt CD4017B Q5 Q1 Q0 Q2 Q6 Q7 Q3 VGND Q8 Q4 Q9 COUT CKEN CLK RST VCC
*
* 74DFLOP *
.SUBCKT 74DFLOP 1PREBAR 1CLRBAR 1CLK 1D 1Q 1QBAR
+     optional:  DPWR=$G_DPWR DGND=$G_DGND
+     params:  MNTYMXDLY=0 IO_LEVEL=0
U1 DFF(1) DPWR DGND
+     1PREBAR 1CLRBAR 1CLK 1D 1Q 1QBAR
+     DLY_HC74 IO_HC MNTYMXDLY={MNTYMXDLY} IO_LEVEL={IO_LEVEL}
.model DLY_HC74 ueff(tppcqlhTY=20ns tppcqlhMX=46ns tppcqhlTY=20ns
+                 tppcqhlMX=46ns twpclMN=25ns
+                 tpclkqlhTY=20ns tpclkqlhMX=35ns tpclkqhlTY=20ns
+                 tpclkqhlMX=35ns twclkhMN=20ns twclklMN=20ns
+                 tsudclkMN=25ns tsupcclkhMN=6ns)
.ENDS 74DFLOP
*
* INV *
.SUBCKT INV A S VGND VCC
XNAND A A S VGND VCC NAND
.ENDS
*
* OR *
.SUBCKT OR A B S VGND VCC
XNOR A B S_ VGND VCC NOR
XNO S_ S VGND VCC INV
.ENDS
*
* AND *
.SUBCKT AND A B S VGND VCC
XNAND A B S_ VGND VCC NAND
XNO S_ S VGND VCC INV
.ENDS
*
* NAND *
.SUBCKT NAND A B S VGND VCC
D1 EQ3 S DDS
D2 VGND A DDS
D3 VGND B DDS
D4 S VCC DDS
R1 VCC BQ1 4000
R2 VCC BQ3 1600
R3 VCC CQ3 130
R4 VGND BQ4 1000
Q1 BQ2 BQ1 A NP
Q1B BQ2 BQ1 B NP
Q2 BQ3 BQ2 BQ4 NP
Q3 CQ3 BQ3 EQ3 NP
Q4 S BQ4 VGND NP
* Q c b e
.model NP NPN(BF=125 Cje=.5p Cjc=.5p Rb=500)
.model PN PNP(BF=25 Cje=.3p Cjc=1.5p Rb=250)
* D
.model DDS D
+IS=5.9569E-12
+N=1.1114
+RS=8.6439E-3
+IKF=0.41263
+CJ0=95.567E-12
+M=0.46779
+VJ=0.78156
+BV=60
+TT=27n
.ends
*
* NOR *
.SUBCKT NOR A B S VGND VCC
D1 VGND A DDS
D2 VGND B DDS
D3 EQ5 S DDS
D4 S VCC DDS
R1 VCC BQ1 4000
R2 VCC BQ2 4000
R3 VCC BQ5 1600
R4 VGND BQ6 1000
R5 VCC CQ5 130
Q1 BQ3 BQ1 A NP
Q2 BQ4 BQ2 B NP
Q3 BQ5 BQ3 BQ6 NP
Q4 BQ5 BQ4 BQ6 NP
Q5 CQ5 BQ5 EQ5 NP
Q6 S BQ6 VGND NP
* Q c b e
.model NP NPN(BF=125 Cje=.5p Cjc=.5p Rb=500)
.model PN PNP(BF=25 Cje=.3p Cjc=1.5p Rb=250)
* D
.model DDS D
+IS=5.9569E-12
+N=1.1114
+RS=8.6439E-3
+IKF=0.41263
+CJ0=95.567E-12
+M=0.46779
+VJ=0.78156
+BV=60
+TT=27n
.ends

* RST*
XINVRST RST _RST VGND VCC INV
RC1 _RST _R 100
CC1 VGND _R 1n

* CLK / EN *
XINVCLK  CLK   _CLK  VGND VCC INV
XINVCKEN CKEN  _CKEN VGND VCC INV
XNANDCLK _CKEN _CLK  C1 VGND VCC NAND

* DFLOP COUNTER *
XUA  _SA _R C1 A C B 74DFLOP
XUB  _SB _R C1 C E D 74DFLOP
XUC  _SC _R C1 L G F 74DFLOP
XUD  _SB _R C1 G I H 74DFLOP
XUE  _SC _R C1 I J A 74DFLOP

* DECODER *
XORK   B F K VGND VCC OR
XNANDL D K L VGND VCC NAND
 
XANDQ0 A B Q0 VGND VCC AND
XANDQ1 C D Q1 VGND VCC AND
XANDQ2 E F Q2 VGND VCC AND
XANDQ3 G H Q3 VGND VCC AND
XANDQ4 I A Q4 VGND VCC AND
XANDQ5 C J Q5 VGND VCC AND
XANDQ6 B E Q6 VGND VCC AND
XANDQ7 D G Q7 VGND VCC AND
XANDQ8 F I Q8 VGND VCC AND
XANDQ9 H J Q9 VGND VCC AND
XINVCOUT J COUT VGND VCC INV

.ends

